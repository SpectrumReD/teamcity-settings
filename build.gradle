
buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.3'
        classpath 'com.github.rodm:gradle-teamcity-plugin:1.0'
        classpath 'com.github.rodm:gradle-teamcity-dsl-plugin:0.5'
    }
}

apply plugin: 'kotlin'
apply plugin: 'com.github.rodm.teamcity-server'
apply plugin: 'com.github.rodm.teamcity-dsl'

ext {
    downloadsDir = findProperty('downloads.dir') ?: "$rootDir/downloads"
    serversDir = findProperty('servers.dir') ?: "$rootDir/servers"
    java8Home = findProperty('java8.home') ?: '/opt/jdk1.8.0_121'
    teamcityServer = findProperty('teamcity.server') ?: 'localhost'
}

repositories {
    maven {
        url "http://${teamcityServer}:8111/app/dsl-plugins-repository"
    }
}

sourceSets.teamcity.kotlin.filter.include('**/*.kts')

teamcity {
    environments {
        baseHomeDir = project.serversDir

        teamcity10 {
            version = '10.0.5'
            javaHome = file(java8Home)
        }

        'teamcity2017.1' {
            version = '2017.1.3'
            javaHome = file(java8Home)
        }
    }
}
